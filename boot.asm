; boot.asm - Single-file MBR Bootloader
[org 0x7c00]
KERNEL_OFFSET equ 0x1000

    mov [BOOT_DRIVE], dl
    mov bp, 0x9000
    mov sp, bp

    call load_kernel   
    call switch_to_pm  
    jmp $

[bits 16]
load_kernel:
    mov bx, KERNEL_OFFSET 
    mov dh, 15            
    mov dl, [BOOT_DRIVE]
    mov ah, 0x02 
    mov al, dh
    mov ch, 0x00 
    mov dh, 0x00 
    mov cl, 0x02 
    int 0x13
    ret

switch_to_pm:
    cli                     
    lgdt [gdt_descriptor]   
    mov eax, cr0
    or eax, 0x1             
    mov cr0, eax
    jmp CODE_SEG:init_pm    

[bits 32]
init_pm:
    mov ax, DATA_SEG        
    mov ds, ax
    mov ss, ax
    mov es, ax
    mov fs, ax
    mov gs, ax
    mov ebp, 0x90000
    mov esp, ebp
    call KERNEL_OFFSET      
    jmp $

; --- GDT Setup ---
gdt_start:
    dq 0x0
gdt_code:
    dw 0xffff, 0x0000
    db 0x00, 0x9a, 0xcf, 0x00
gdt_data:
    dw 0xffff, 0x0000
    db 0x00, 0x92, 0xcf, 0x00
gdt_end:
gdt_descriptor:
    dw gdt_end - gdt_start - 1
    dd gdt_start
CODE_SEG equ gdt_code - gdt_start
DATA_SEG equ gdt_data - gdt_start

BOOT_DRIVE db 0
times 510-($-$$) db 0
dw 0xaa55